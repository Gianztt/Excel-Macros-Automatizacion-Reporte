# üìä Automatizaci√≥n de Reportes en Excel con Macros (VBA)

<img width="1773" height="1010" alt="image" src="https://github.com/user-attachments/assets/6665c213-3d48-4b94-8567-cc5eabc21fe0" />

Este proyecto consiste en una herramienta desarrollada en **Excel con Macros (VBA)** que permite automatizar completamente el proceso de:

- Consolidar m√∫ltiples archivos de ventas.
- Calcular KPIs clave de forma autom√°tica.
- Generar una tabla din√°mica y un gr√°fico.
- Ejecutar todo el proceso con un solo bot√≥n.

Dise√±ada como una soluci√≥n pr√°ctica para **Control de Gesti√≥n**, esta herramienta es especialmente √∫til para reportes mensuales o semanales.

---

## üóÇÔ∏è Datos utilizados

Se trabaj√≥ con tres archivos de ventas ficticios correspondientes a los meses de **enero, febrero y marzo**, ubicados en la carpeta `/Datos_Ventas`.

---

## ‚öôÔ∏è Paso a paso del proyecto

### 1. Macro de Consolidaci√≥n de Archivos

Se desarroll√≥ una macro que recorre autom√°ticamente todos los archivos de la carpeta y **consolida la informaci√≥n en una sola hoja de c√°lculo**, unificando las ventas de los tres meses.

```vba
Sub ConsolidarVentas()
    Dim Carpeta As String, Archivo As String
    Dim LibroOrigen As Workbook
    Dim HojaDestino As Worksheet
    Dim UltimaFila As Long, FilaInicio As Long

    ' Ruta de la carpeta
    Carpeta = ThisWorkbook.Path & "\Datos_Ventas\"
    Archivo = Dir(Carpeta & "*.xlsm")

    ' Hoja donde ir√° todo
    Set HojaDestino = ThisWorkbook.Sheets("Ventas")
    HojaDestino.Cells.Clear
    FilaInicio = 2

    ' Copiar encabezado
    HojaDestino.Range("A1:G1").Value = Array("Fecha", "Regi√≥n", "Producto", "Unidades", "Precio", "Descuento", "Costo Unitario")

    Do While Archivo <> ""
        Set LibroOrigen = Workbooks.Open(Carpeta & Archivo)
        With LibroOrigen.Sheets("Ventas")
            UltimaFila = .Cells(.Rows.Count, "A").End(xlUp).Row
            .Range("A2:G" & UltimaFila).Copy HojaDestino.Cells(FilaInicio, 1)
            FilaInicio = HojaDestino.Cells(HojaDestino.Rows.Count, "A").End(xlUp).Row + 1
        End With
        LibroOrigen.Close SaveChanges:=False
        Archivo = Dir
    Loop
    
        ' Agregar encabezados
    HojaDestino.Range("H1").Value = "Margen"
    HojaDestino.Range("I1").Value = "Ingreso Total"
    HojaDestino.Range("J1").Value = "Descuento Total"
    
    ' Aplicar f√≥rmulas
    Dim UltimaFilaDatos As Long
    UltimaFilaDatos = HojaDestino.Cells(HojaDestino.Rows.Count, "A").End(xlUp).Row
    
    Dim i As Long
    For i = 2 To UltimaFilaDatos
        ' Margen
        HojaDestino.Cells(i, "H").Formula = "=(E" & i & " - G" & i & ") / E" & i
        ' Ingreso Total
        HojaDestino.Cells(i, "I").Formula = "=E" & i & "*D" & i
        ' Descuento Total
        HojaDestino.Cells(i, "J").Formula = "=E" & i & "*F" & i & "*D" & i
    Next i

    MsgBox "Consolidaci√≥n completada", vbInformation
    
End Sub
```

<img width="1452" height="776" alt="image" src="https://github.com/user-attachments/assets/ab465ec8-b543-4492-aba2-c39d07a5f9a1" />


### 2. Macro de Tabla Din√°mica

Se cre√≥ una macro que genera **una tabla din√°mica autom√°ticamente** con las m√©tricas relevantes a partir de los datos consolidados. Esta tabla sirve como base para el an√°lisis.

```vba
Sub CrearTablaDinamica()
    Dim ptCache As PivotCache
    Dim pt As PivotTable
    Dim wsDatos As Worksheet
    Dim wsResumen As Worksheet
    Dim UltimaFila As Long
    Dim RangoDatos As Range

    ' Set hojas
    Set wsDatos = ThisWorkbook.Sheets("Ventas")

    ' Crear hoja para resumen
    On Error Resume Next
    Application.DisplayAlerts = False
    ThisWorkbook.Sheets("Resumen").Delete
    Application.DisplayAlerts = True
    On Error GoTo 0

    Set wsResumen = ThisWorkbook.Sheets.Add
    wsResumen.Name = "Resumen"

    ' Determinar √∫ltima fila
    UltimaFila = wsDatos.Cells(wsDatos.Rows.Count, "A").End(xlUp).Row
    Set RangoDatos = wsDatos.Range(wsDatos.Cells(1, 1), wsDatos.Cells(UltimaFila, 10)) ' Columnas A a J

    ' Crear cach√©
    Set ptCache = ThisWorkbook.PivotCaches.Create( _
        SourceType:=xlDatabase, _
        SourceData:=RangoDatos)

    ' Crear tabla din√°mica
    Set pt = ptCache.CreatePivotTable( _
        TableDestination:=wsResumen.Range("A3"), _
        TableName:="VentasPorRegion_" & Format(Now, "hhmmss"))

    ' Esperar breve momento (opcional, para estabilidad)
    Application.Wait Now + TimeValue("0:00:01")

    ' Configurar tabla din√°mica
    With pt
        .PivotFields("Regi√≥n").Orientation = xlRowField
        With .PivotFields("Ingreso Total")
            .Orientation = xlDataField
            .Function = xlSum
            .NumberFormat = "#,##0"
        End With
    End With
End Sub
```

<img width="1497" height="816" alt="image" src="https://github.com/user-attachments/assets/dbc73ba9-cd14-48ba-be4a-fe8994aaf98c" />


### 3. Macro de Gr√°fico Din√°mico

A partir de la tabla din√°mica, se automatiz√≥ la **generaci√≥n de un gr√°fico de barras**, lo que permite visualizar los resultados de forma clara e inmediata.

```vba
Sub CrearGraficoDinamico()
    Dim wsResumen As Worksheet
    Dim pt As PivotTable
    Dim grafico As ChartObject

    Set wsResumen = ThisWorkbook.Sheets("Resumen")
    Set pt = wsResumen.PivotTables(1) ' Usa la primera tabla din√°mica

    ' Borrar gr√°ficos existentes si los hay
    For Each grafico In wsResumen.ChartObjects
        grafico.Delete
    Next grafico

    ' Insertar nuevo gr√°fico
    Set grafico = wsResumen.ChartObjects.Add(Left:=300, Top:=30, Width:=500, Height:=300)
    With grafico.Chart
        .SetSourceData Source:=pt.TableRange1
        .ChartType = xlColumnClustered
        .HasTitle = True
        .ChartTitle.Text = "Ventas Totales por Regi√≥n"
        .ApplyLayout (4)
    End With
End Sub
```

<img width="1125" height="508" alt="image" src="https://github.com/user-attachments/assets/92660e90-3ca9-4f01-a893-bcd24c7e5606" />


### 4. Macro de Automatizaci√≥n Total

Se desarroll√≥ una macro principal que permite ejecutar todo el proceso anterior con **un solo bot√≥n interactivo**, facilitando el uso para usuarios sin conocimientos de VBA.

```vba
Sub GenerarReporteCompleto()
    Call ConsolidarVentas
    Call CrearTablaDinamica
    Call CrearGraficoDinamico
End Sub
```

<img width="431" height="239" alt="image" src="https://github.com/user-attachments/assets/9704589b-f20c-49d2-b932-cf751a01d2d2" />


---

## üß† Aplicaci√≥n pr√°ctica

Este proyecto demuestra c√≥mo aplicar **automatizaci√≥n con VBA en tareas reales de control de gesti√≥n**, permitiendo:

- Ahorro significativo de tiempo.
- Reducci√≥n de errores manuales.
- Estandarizaci√≥n de reportes.

---

## üìÅ Archivos incluidos

- `macro_reportes.xlsm`: archivo principal con macros y bot√≥n de ejecuci√≥n.
- Carpeta `/Datos_Ventas`: contiene los archivos de ventas mensuales.
- `README.md`: descripci√≥n detallada del proyecto.

---

## üß∞ Tecnolog√≠as utilizadas

- Microsoft Excel
- VBA (Visual Basic for Applications)
